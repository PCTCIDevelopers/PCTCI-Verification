%% CTP: B2 - intermediar (B1,B2,B4,PG)


%%%% broker1 (B1,B2,PG,Cn1,Otp1,Pid1,Pid2,N01,Am1,Am2,PkB1,PkB2,PkPG,H,Snd,Rcv)
role broker1 (B1, B2, PG: agent,             
            Cn1, Otp1, Pid1, Pid2, N01: text,	
            Am1, Am2: nat,	
            PkB1, PkB2, PkPG: public_key,  
            H: hash_func,	
            Snd, Rcv: channel(dy))
played_by B1 def=

  local State:nat, 
        N12, N24:text,
        Kb1b2, Kb1pg: symmetric_key  
        
  init State := 0

  transition

% B1 trimite PO12 la B2 intermediar
 1.  State = 0 /\ Rcv(start)
          =|> 
          State' :=1
	  /\ N12' :=new() 
          /\ Kb1b2' :=new() 
          /\ Snd({{B1.Cn1.Otp1.N01.N12'.Am1.or.Am2.B2.{H(B1.Cn1.Otp1.N01.N12'.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.B1.B2.Pid1.or.Pid2.N01.N12'.Am1.or.Am2.{H(B1.B2.Pid1.or.Pid2.N01.N12'.Am1.or.Am2)}_inv(PkB1)}_Kb1b2'.{Kb1b2'}_PkB2)
          /\ secret(Cn1,scn1,{B1,PG})
          /\ witness(B1,PG,pg_b1_pi1,B1.Cn1.Otp1.N01.N12'.Am1.or.Am2.B2)
          /\ witness(B1,B2,b2_b1_oi1,B1.B2.Pid1.or.Pid2.N01.N12'.Am1.or.Am2)

% B1 receptioneaza PE12=y, si E24barat=y de la B2 
 2.  State = 1 
          /\ Rcv({y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).y.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG).y.N01.N12.N24'.Pid1.{H(y.N01.N12.N24'.Pid1)}_inv(PkPG)}_Kb1b2)
          =|> 
          State' :=2         
          /\ request(B1,PG,b1_pg_pe12,y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1))

%--R1.1: B1 receptioneaza PE12=a, de la B2
 2.  State = 1 
          /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb1b2)
          =|> 
          State' :=3        
          /\ request(B1,PG,b1_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))

%--R1.2: B1 receptioneaza PE12=a, de la PG
 3.  State = 1 
          /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkB1)
          =|> 
          State' :=4       
          /\ request(B1,PG,b1_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))

%--Res1: B1 receptioneaza APE12, de la PG
 4.  State = 2 /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkPG)
          =|> 
          State' :=5 
	  /\ request(B1,PG,b1_pg_ape12,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1)).H(a.N01.N12.Pid1.H(y.N01.N12.Pid1))) 

%-- R2.1: B1 receptioneaza PE12=a de la PG ptr pid1 or pid2
 5.  State  = 1 /\ Rcv({a.B1.B2.N01.N12.Pid1.or.Pid2.{H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2)}_inv(PkPG).a.N01.N12.Pid1.or.Pid2.{H(a.N01.N12.Pid1.or.Pid2)}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkPG)
          =|> 
          State':= 6
          /\ request(B1,PG,b1_pg_pe12a_pid1_pid2,a.B1.B2.N01.N12.Pid1.or.Pid2.H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2).H(a.N01.N12.Pid1.or.Pid2))
end role

%%%%% broker2 (B1,B2,B4,PG,Cn2,Otp2,PkB1,PkB2,PkB4,PkPG,OIList1,H,Snd,Rcv)
role broker2 (B1, B2, B4, PG: agent,
            Cn2,Otp2:text,  	            
            PkB1, PkB2, PkB4, PkPG: public_key, 
 	    OIList1: (agent.agent.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,
            H: hash_func,	
            Snd, Rcv: channel(dy))
played_by B2 def=

  local State: nat,  
          Am1, Am2: nat, 	 
          N01, N12, N24, N45,Pid1, Pid2: text, 	
          Kb1b2, Kb2b4, Kb2pg: symmetric_key,
	  Y: {agent.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_public_key

  init State := 0

  transition

% B2 rec PO12 de la B1 si trim PO24 la B4
 1.  State = 0 /\ Rcv({Y'.B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2'.{H(B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2')}_inv(PkB1)}_Kb1b2'.{Kb1b2'}_PkB2) 
	  /\ not(in(B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2'.{H(B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2')}_inv(PkB1), OIList1)) 
         % testul se face numai pe prod individual, nu pe produs   /\ in(Pid1'.or.Pid2'.Am1'.or.Am2' , PList) % B2 este provider
          =|> 
          State' :=1 
	  /\ OIList1' :=cons(B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2'.{H(B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2')}_inv(PkB1) , OIList1)	
	  /\ N24' :=new()
          /\ Kb2b4' :=new()
	  /\ Snd({{B2.Cn2.Otp2.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2.Otp2.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2'.{H(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')}_inv(PkB2)}_Kb2b4'.{Kb2b4'}_PkB4)
          /\ request(B2,B1,b2_b1_oi1,B1.B2.Pid1'.or.Pid2'.N01'.N12'.Am1'.or.Am2')
          /\ secret(Cn2,scn2,{B2,PG})
          /\ witness(B2,PG,pg_b2_pi2,B2.Cn2.Otp2.N01'.N12'.N24'.Am1'.or.Am2'.B4)
          /\ witness(B2,B4,b4_b2_oi2,B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')

% B2 receptioneaza PE24=y si E46barat=y de la B4,atunci trim PR12 la PG    
 2.  State = 1 /\ Rcv({y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG).y.N01.N12.N24.N45'.Pid1.{H(y.N01.N12.N24.N45'.Pid1)}_inv(PkPG)}_Kb2b4) 
         =|> 
         State':= 2
         /\ Kb2pg' :=new()
         /\ Snd({Y.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2)}_Kb2pg'.{Kb2pg'}_PkPG)
        
% B2 rec de la PG PE12=y, pe care o trm  impreuna cu E24barat=E24=y la B1              
 3.  State = 2 /\ Rcv({y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).y.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg)
          =|> 
          State':= 3
          /\ Snd({y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).y.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb1b2)
          /\ request(B2,PG,b2_pg_pe12,y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1))
 	  /\ request(B2,PG,b2_pg_pe24,y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1))

%--R1.1: B2 rec de la PG PE12=a, pe care o trim la B1 si trimite PE12=a,PE24=y la PG ptr a aborta s24             
 4.  State = 2 /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg)
          =|> 
          State':= 4
          /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb1b2)
	  /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG).y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg)
         
%--R1.1: B2 receptioneaza raspunsul de la PG cu APE24=a in s24 care era y anterior
 5.  State = 4 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg)
         =|> 
         State':= 5  
         /\ request(B2,PG,b2_pg_ape24,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1)).H(a.N01.N12.N24.Pid1.H(y.N01.N12.N24.Pid1)))       
         /\ request(B2,PG,b2_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))  
          %B accepta ape24=a (dupa ce pe24=y),  si pe12=a in mom receptei ape24

%--R1.2  B2 rec PE24=a de la B4 (s24=a)si trimite la PG PE24=a,PM1,OI1 ptr a obtine s12=a
 6.  State = 1 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2b4)
         =|> 
         State':= 6 
	  /\ Kb2pg' :=new()
	  /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG).Y.B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2.{H(B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2)}_inv(PkB1)}_Kb2pg'.{Kb2pg'}_PkPG)

%--R1.2: B2 rec de la PG PE12=a 
 7.  State = 6 /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg)
          =|> 
          State':= 7
          /\ request(B2,PG,b2_pg_pe24a,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1)).H(a.N01.N12.N24.Pid1.H(y.N01.N12.N24.Pid1)))       
          /\ request(B2,PG,b2_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))  
          %B accepta pe24=a si pe12=a in mom receptei pe12=a

%--Res1 din spate din ATP de la B1: B2 rec APE12 de la PG in R1 dupa ce  PE24=y, PE12=y (B2 in state=3) si trm APE12 si PE24 =y la PG ptr a aborta PE24
 8.  State = 3 /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg)
        =|> 
        State' :=8 
       	/\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg)

%--Res1 din spate din ATP de la B1: B2 rec APE24 de la PG in R1 dupa ce  PE24=y, PE12=y 
 9.  State = 8 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg) 
        =|> 
          State' :=9 
       	  /\ request(B2,PG,b2_pg_ape24,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1)).H(a.N01.N12.N24.Pid1.H(y.N01.N12.N24.Pid1)))   
          /\ request(B2,PG,b2_pg_ape12,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1)).H(a.N01.N12.Pid1.H(y.N01.N12.Pid1)))  
 	 %B2 accepta ape24=a. ape12=a in mom receptei ape24

%--R2.1: B2 (in starea 1 dupa trim po24) receptioneaza t de la intrus ptr a simula ca PE24=a trimis de B4 nu mai ajunge la B2(cadere retea, intervine intrusul,etc., comp neonest al lui B4 care a rec PE24=a si nu mai trimite la B2) si trimite la PG PM2.OI2 la PG ptr a recept PE24 aborted 
 10.  State = 1 /\ Rcv(t)
         =|> 
        State':= 10
	 /\ Kb2pg' :=new()
         /\ Snd({{B2.Cn2.Otp2.N01.N12.N24.Am1.or.Am2.B4.{H(B2.Cn2.Otp2.N01.N12.N24.Am1.or.Am2.B4)}_inv(PkB2)}_PkPG.B2.B4.Pid1.or.Pid2.N01.N12.N24.Am1.or.Am2.{H(B2.B4.Pid1.or.Pid2.N01.N12.N24.Am1.or.Am2)}_inv(PkB2)}_Kb2pg'.{Kb2pg'}_PkPG)

%--R2.1: B2 receptioneaza PE24=a de la PG in R2 si trimite la PG PE24=a,PM1,OI1 ptr a obtine s12=a
 11.  State = 10 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg)
         =|> 
        State':= 11
	  /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG).Y.B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2.{H(B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2)}_inv(PkB1)}_Kb2pg) 

%--R2.1  B2 rec PE12=a de la PG ptr pid1 or pid2 
 12.  State = 11 /\ Rcv({a.B1.B2.N01.N12.Pid1.or.Pid2.{H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2)}_inv(PkPG).a.N01.N12.Pid1.or.Pid2.{H(a.N01.N12.Pid1.or.Pid2)}_inv(PkPG)}_Kb2pg)
         =|> 
         State':= 12
 	  /\ request(B2,PG,b2_pg_pe24a,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1).H(a.B2.B4.N01.N12.Pid1))
          /\ request(B2,PG,b2_pg_pe12a_pid1_pid2,a.B1.B2.N01.N12.Pid1.or.Pid2.H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2).H(a.N01.N12.Pid1.or.Pid2))
          %B2 accepta pe24=a. pe12=a in mom receptei pe12=a
end role

%%%%% broker4 (B2,B4,PG,PkB2,PkB4,PkPG,OIList2,H,Snd,Rcv)
role broker4 (B2, B4, PG: agent,	            
            PkB2, PkB4, PkPG: public_key, 
 	    OIList2: (agent.agent.text.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,
            H: hash_func,	
            Snd, Rcv: channel(dy))
played_by B4 def=

  local State: nat,  
          Am1, Am2: nat, 	 
          N01, N12, N24, N45,Pid1, Pid2: text, 	
          Kb2b4, Kb4pg: symmetric_key,
	  Z: {agent.text.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_public_key

  init State := 0

  transition

% B4 rec PO24 de la B2 si trim PR24 la PG ce include Pid1 ptr ca pp ca Pid1 e cump cu succes in ot46
 1.  State = 0 /\ Rcv({Z'.B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2'.{H(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')}_inv(PkB2)}_Kb2b4'.{Kb2b4'}_PkB4) 
	  /\ not(in(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2'.{H(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')}_inv(PkB2), OIList2)) 
          =|> 
          State' :=1 
	  /\ N45':=new()
          /\ Kb4pg':=new()
	  /\ OIList2' :=cons(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2'.{H(B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')}_inv(PkB2) , OIList2)
	  /\ Snd({Z'.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4)}_Kb4pg'.{Kb4pg'}_PkPG)
          /\ request(B4,B2,b4_b2_oi2,B2.B4.Pid1'.or.Pid2'.N01'.N12'.N24'.Am1'.or.Am2')

% B4 rec de la PG PE24=y, E46barat=y pe care le trim la B2              
 2.  State = 1 /\ Rcv({y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG).y.N01.N12.N24.N45.Pid1.{H(y.N01.N12.N24.N45.Pid1)}_inv(PkPG)}_Kb4pg)
          =|> 
          State':= 2
          /\ Snd({y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG).y.N01.N12.N24.N45.Pid1.{H(y.N01.N12.N24.N45.Pid1)}_inv(PkPG)}_Kb2b4)
    
%--R1.1: B4 rec de la PG APE24=a, dupa ce PE24 a fost =y
 3.  State = 2 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb4pg)
         =|> 
        State':= 3

%--R1.2: B4 rec de la PG PE24=a, si o trim la B2              
 4.  State = 1 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb4pg)
          =|> 
          State':= 4
          /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2b4)

%--Res1: B4 rec de la PG APE24=a, dupa ce PE24 a fost =y
 5.  State = 2 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb4pg) 
         =|> 
        State':= 5
end role

%%%%%%payment gateway
role paymentgateway (B1, B2, B4, PG: agent,             
            PkB1, PkB2, PkB4, PkPG: public_key,  
            CIList: (agent.text.text) set,
%{B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2)
           PRList2: ({agent.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.{hash(text.text.text.agent.agent.nat)}_inv(public_key)) set,
%B2.Cn2.Otp2.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2.Otp2.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4)
	    PRList4: ({agent.text.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.text.{hash(text.text.text.text.text.agent.agent.nat)}_inv(public_key)) set,
            H: hash_func,	
            Snd, Rcv: channel(dy))
played_by PG def=

  local State: nat,
          Cn1, Otp1, Cn2, Otp2: text,	
          Am1, Am2: nat,	
          N01, N12, N24, N45, Pid1, Pid2: text,
          Kb1pg, Kb2pg, Kb4pg: symmetric_key
	
  init State := 0

  transition

% PG recept PR24 de la B4 si trimite PE24=y si E46barat=y (ptr a simula ca B4 a receptionat E46=y ptr Pid1) la B4
 1.  State = 0 
          /\ Rcv({{B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4)}_Kb4pg'.{Kb4pg'}_PkPG) 
          /\ not(in({B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4), PRList4))  
          /\ in(B2.Cn2'.Otp2' , CIList)  
        =|> 
          State' :=1 
          /\ PRList4' :=cons({B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4) , PRList4)
          /\ Snd({y.B2.B4.N01'.N12'.N24'.Pid1'.{H(y.B2.B4.N01'.N12'.N24'.Pid1'.Am1')}_inv(PkPG).y.N01'.N12'.N24'.Pid1'.{H(y.N01'.N12'.N24'.Pid1')}_inv(PkPG).y.N01'.N12'.N24'.N45'.Pid1'.{H(y.N01'.N12'.N24'.N45'.Pid1')}_inv(PkPG)}_Kb4pg')
          /\ request(PG,B2,pg_b2_pi2,B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)
 	  /\ witness(PG,B2,b2_pg_pe24,y.B2.B4.N01'.N12'.N24'.Pid1'.H(y.B2.B4.N01'.N12'.N24'.Pid1'.Am1').H(y.N01'.N12'.N24'.Pid1'))

% PG recept PR12 de la B2 si trimite PE12=y la B2
 2.  State = 1 
          /\ Rcv({{B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2)}_Kb2pg'.{Kb2pg'}_PkPG) 
          /\ not(in({B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2), PRList2))  
%PRList2: ({agent.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.{hash(text.text.text.agent.agent.nat)}_inv(public_key)) set,
          /\ in(B1.Cn1'.Otp1' , CIList)  
        =|> 
          State' :=2 
          /\ PRList2' :=cons({B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2) , PRList2)

          /\ Snd({y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).y.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg')
          /\ request(PG,B1,pg_b1_pi1,B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)
          /\ witness(PG,B2,b2_pg_pe12,y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1))
 	  /\ witness(PG,B1,b1_pg_pe12,y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1))

%--R1.1: PG dupa ce PE24=y, recept PR12 de la B2 si trimite PE12=a la B2
 3.  State = 1 
 	   /\ Rcv({{B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2)}_Kb2pg'.{Kb2pg'}_PkPG) 
          /\ not(in({B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2), PRList2))  
          /\ not(in(B1.Cn1'.Otp1' , CIList))
        =|> 
          State' :=3
          /\ PRList2' :=cons({B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.Pid1.{H(Pid1.N01.N12.B1.B2.Am1)}_inv(PkB2) , PRList2)
	  /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg')  
          /\ witness(PG,B2,b2_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))
 	  /\ witness(PG,B1,b1_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))
        
%--R1.1: PG receptioneaza de la B2 PE12=a, PE24=y si trimite APE24 la B4,B2;  
 4.  State = 3 
	  /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG).y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg)
         =|> 
         State':= 4  
       /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb4pg)    %trimite APE24 la B4
       /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg) %trimite APE45 la B2
       /\ witness(PG,B2,b2_pg_ape24,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1)).H(a.N01.N12.N24.Pid1.H(y.N01.N12.N24.Pid1))) 

%--R1.2: PG recept PR24 de la B4 si trimite PE24=a la B4
 5.  State = 0 
          /\ Rcv({{B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4)}_Kb4pg'.{Kb4pg'}_PkPG) 
          /\ not(in({B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4), PRList4))  
          /\ not(in(B2.Cn2'.Otp2' , CIList))
        =|> 
          State' :=5 
          /\ PRList4' :=cons({B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4.{H(B2.Cn2'.Otp2'.N01'.N12'.N24'.Am1'.or.Am2'.B4)}_inv(PkB2)}_PkPG.Pid1'.N45'.{H(Pid1'.N01'.N12'.N24'.N45'.B2.B4.Am1')}_inv(PkB4) , PRList4)
          /\ Snd({a.B2.B4.N01'.N12'.N24'.Pid1'.{H(a.B2.B4.N01'.N12'.N24'.Pid1'.Am1')}_inv(PkPG).a.N01'.N12'.N24'.Pid1'.{H(a.N01'.N12'.N24'.Pid1')}_inv(PkPG)}_Kb4pg')
 	  /\ witness(PG,B2,b2_pg_pe24a,a.B2.B4.N01'.N12'.N24'.Pid1'.H(a.B2.B4.N01'.N12'.N24'.Pid1'.Am1').H(a.N01'.N12'.N24'.Pid1'))

%--R1.2: PG recept PE24=a,PM1,OI1 de la B2 si trimite PE12=a la B2 si B1
 6.  State = 5 
 	   /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG).{B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG.B1.B2.Pid1.or.Pid2'.N01.N12.Am1.or.Am2.{H(B1.B2.Pid1.or.Pid2'.N01.N12.Am1.or.Am2)}_inv(PkB1)}_Kb2pg'.{Kb2pg'}_PkPG)
        =|> 
          State' :=6
	  /\ Kb1pg':=new()
	  /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb2pg')
 	  /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).a.N01.N12.Pid1.{H(a.N01.N12.Pid1)}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkB1)
          /\ witness(PG,B2,b2_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))
 	  /\ witness(PG,B1,b1_pg_pe12a,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1).H(a.N01.N12.Pid1))

%--Res1 din spate din ATP de la B1:
% PG in State 2 dupa ce a gen PE24=y, PE12=y, recept evenim "Res1" de la intrus in Intruder simulation ce reprezinta ca PG a rec In R1 de la B1 in ATP: PE01=a,PE12=y si atunci PG trimite APE12 la B1,B2
 7.  State = 2 /\ Rcv(res1) 
        =|> 
          State' :=7 
	  /\ Kb1pg' :=new()
	  /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkPG) 
          /\ Snd({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg)
          /\ witness(PG,B2,b2_pg_ape12,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1)).H(a.N01.N12.Pid1.H(y.N01.N12.Pid1)))
          /\ witness(PG,B1,b1_pg_ape12,a.B1.B2.N01.N12.Pid1.H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.H(y.B1.B2.N01.N12.Pid1.Am1).H(y.N01.N12.Pid1)).H(a.N01.N12.Pid1.H(y.N01.N12.Pid1)))

%--Res1 din spate din ATP de la B2: %PG rec APE12 si PE24 =y  de la B2 si trm APE24 la B2, B4
 8.  State = 7 /\ Rcv({a.B1.B2.N01.N12.Pid1.{H(a.B1.B2.N01.N12.Pid1.Am1.y.B1.B2.N01.N12.Pid1.{H(y.B1.B2.N01.N12.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.Pid1.{H(y.N01.N12.Pid1)}_inv(PkPG))}_inv(PkPG).y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).y.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg)
          =|> 
          State' :=8
	  /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb2pg) %trm APE24 la B2
 	  /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.{H(y.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG).{H(a.N01.N12.N24.Pid1.{H(y.N01.N12.N24.Pid1)}_inv(PkPG))}_inv(PkPG)}_Kb4pg) %trm APE24 la B4
	  /\ witness(PG,B2,b2_pg_ape24,a.B2.B4.N01.N12.N24.Pid1.H(a.B2.B4.N01.N12.N24.Pid1.Am1.y.B2.B4.N01.N12.N24.Pid1.H(y.B2.B4.N01.N12.N24.Pid1.Am1).H(y.N01.N12.N24.Pid1)).H(a.N01.N12.N24.Pid1.H(y.N01.N12.N24.Pid1))) 

%--R2.1: PG receptioneaza cererea in R2 de la B2 ce contine PM2, OI2 si trimite PE24=a (pe care a generat-o in tranzitia 5, starea 5, si am witness(PG,B4...)deci nu mai trebuie pus aici) la B2,B4 
 9.  State = 5 /\ Rcv({{B2.Cn2.Otp2.N01.N12.N24.Am1.or.Am2.B4.{H(B2.Cn2.Otp2.N01.N12.N24.Am1.or.Am2.B4)}_inv(PkB2)}_PkPG.B2.B4.Pid1.or.Pid2'.N01.N12.N24.Am1.or.Am2.{H(B2.B4.Pid1.or.Pid2'.N01.N12.N24.Am1.or.Am2)}_inv(PkB2)}_Kb2pg'.{Kb2pg'}_PkPG)
         =|> 
        State':= 9
          /\ Snd({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG)}_Kb2pg')
 %trimite PE24=a la B2 (PE24=a este recept de B4)

%--R2.1:  PG rec de la B2 PE24=a,PM1,OI1 si trimite PE12=a la B2 si B1                
 10.  State = 9 /\ Rcv({a.B2.B4.N01.N12.N24.Pid1.{H(a.B2.B4.N01.N12.N24.Pid1.Am1)}_inv(PkPG).a.N01.N12.N24.Pid1.{H(a.N01.N12.N24.Pid1)}_inv(PkPG).
{B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2.{H(B1.Cn1'.Otp1'.N01.N12.Am1.or.Am2.B2)}_inv(PkB1)}_PkPG
.B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2.{H(B1.B2.Pid1.or.Pid2.N01.N12.Am1.or.Am2)}_inv(PkB1)}_Kb2pg) 
         =|> 
         State':= 10 
	  /\ Kb1pg' :=new()
          /\ Snd({a.B1.B2.N01.N12.Pid1.or.Pid2.{H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2)}_inv(PkPG).a.N01.N12.Pid1.or.Pid2.{H(a.N01.N12.Pid1.or.Pid2)}_inv(PkPG)}_Kb2pg)
          /\ Snd({a.B1.B2.N01.N12.Pid1.or.Pid2.{H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2)}_inv(PkPG).a.N01.N12.Pid1.or.Pid2.{H(a.N01.N12.Pid1.or.Pid2)}_inv(PkPG)}_Kb1pg'.{Kb1pg'}_PkPG)
	  /\ witness(PG,B1,b1_pg_pe12a_pid1_pid2,a.B1.B2.N01.N12.Pid1.or.Pid2.H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2).H(a.N01.N12.Pid1.or.Pid2))
	  /\ witness(PG,B2,b2_pg_pe12a_pid1_pid2,a.B1.B2.N01.N12.Pid1.or.Pid2.H(a.B1.B2.N01.N12.Pid1.or.Pid2.Am1.or.Am2).H(a.N01.N12.Pid1.or.Pid2))
end role


%%%%ctp(b1,b2,b4,pg,cn1,otp1,pid1,pid2,cn2,otp2,n01,am1,am2,pkb1,pkb2,pkb4,pkpg,h,OIList1,PList,CIList,PRList1,PRList2,PRList3,Snd,Rcv)
role ctp(B1,B2,B4,PG: agent,           
              Cn1,Otp1,Pid1,Pid2,Cn2,Otp2,N01: text,      
              Am1, Am2: nat,	
              PkB1, PkB2, PkB4, PkPG: public_key,  
              H: hash_func,
	      OIList1: (agent.agent.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,	 
	      OIList2: (agent.agent.text.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,
              CIList: (agent.text.text) set,
              PRList2: ({agent.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.{hash(text.text.text.agent.agent.nat)}_inv(public_key)) set,
	      PRList4: ({agent.text.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.text.{hash(text.text.text.text.text.agent.agent.nat)}_inv(public_key)) set,
              Snd, Rcv: channel(dy))
def=


  composition

       broker1 (B1,B2,PG,Cn1,Otp1,Pid1,Pid2,N01,Am1,Am2,PkB1,PkB2,PkPG,H,Snd,Rcv)
       /\ broker2 (B1,B2,B4,PG,Cn2,Otp2,PkB1,PkB2,PkB4,PkPG,OIList1,H,Snd,Rcv)
       /\ broker4 (B2,B4,PG,PkB2,PkB4,PkPG,OIList2,H,Snd,Rcv)
       /\ paymentgateway (B1,B2,B4,PG,PkB1,PkB2,PkB4,PkPG,CIList,PRList2,PRList4,H,Snd,Rcv)
end role


role environment() def=

  local      OIList1: (agent.agent.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,
	     OIList2: (agent.agent.text.text.text.text.text.text.nat.text.nat.{hash(agent.agent.text.text.text.text.text.text.nat.text.nat)}_inv(public_key)) set,
	     CIList: (agent.text.text) set,
             PRList2: ({agent.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.{hash(text.text.text.agent.agent.nat)}_inv(public_key)) set,
	     PRList4: ({agent.text.text.text.text.text.nat.text.nat.agent.{hash(agent.text.text.text.text.text.nat.text.nat.agent)}_inv(public_key)}_(public_key).text.text.{hash(text.text.text.text.text.agent.agent.nat)}_inv(public_key)) set,    
             Snd, Rcv: channel(dy)

  const b1, b2, b4, pg, i: agent,
              cn1,otp1,cn2,otp2,cni,otpi, pid1,pid2, bg,or,and,en,y,a,t,res1: text,
	      n01,n12,n24: text,	
              am1,am2: nat,	
              pkb1,pkb2,pkb4,pkpg,pki: public_key,  
              h: hash_func,	
              scn1, scn2, b2_b1_oi1, b4_b2_oi2, pg_b1_pi1, pg_b2_pi2, b2_pg_pe24,b2_pg_ape24,b2_pg_pe24a,b2_pg_pe12, b2_pg_pe12a, b2_pg_ape12, b2_pg_pe12a_pid1_pid2,b1_pg_pe12, b1_pg_pe12a, b1_pg_ape12, b1_pg_pe12a_pid1_pid2: protocol_id, 
	      certB2: text             
             
%scn1, scn12, scn13, b1_c_oi0, b2_b1_oi1, b3_b1_oi2,pg_c_pi0,pg_b1_pi1, pg_b1_pi2, b1_pg_pe12, b1_pg_ape12, b1_pg_pe13, b1_pg_ape13,b1_pg_pe13a, b1_pg_pe01,b1_pg_pe01a, c_pg_pe01, c_pg_pe01a: protocol_id, 

  init  
          CIList :={b1.cn1.otp1, b2.cn2.otp2, i.cni.otpi}
	  % CIList :={b2.cn2.otp2, i.cni.otpi} %numai ptr cazul R1.1
	  % CIList :={b1.cn1.otp1, i.cni.otpi} %numai ptr cazul R1.2
          /\ OIList1 := {}
	  /\ OIList2 := {}
          /\ PRList2 := {}
          /\ PRList4 := {}

  intruder_knowledge = {b1, b2, b4, pg, cni, otpi, pkb1, pkb2,pkb4, pkpg, pki, inv(pki), pid1, pid2, am1, am2, t,res1}

  composition
  ctp(b1,b2,b4,pg,cn1,otp1,pid1,pid2,cn2,otp2,n01,am1,am2,pkb1,pkb2,pkb4,pkpg,h,OIList1,OIList2,CIList,PRList2,PRList4,Snd,Rcv)
/\ ctp(i,b2,b4,pg,cni,otpi,pid1,pid2,cn2,otp2,n01,am1,am2,pki,pkb2,pkb4,pkpg,h,OIList1,OIList2,CIList,PRList2,PRList4,Snd,Rcv)
/\ ctp(b1,i,b4,pg,cn1,otp1,pid1,pid2,cni,otpi,n01,am1,am2,pkb1,pki,pkb4,pkpg,h,OIList1,OIList2,CIList,PRList2,PRList4,Snd,Rcv)
/\ ctp(b1,b2,i,pg,cn1,otp1,pid1,pid2,cn2,otp2,n01,am1,am2,pkb1,pkb2,pki,pkpg,h,OIList1,OIList2,CIList,PRList2,PRList4,Snd,Rcv)

	    
end role

% Description of goal properties:
goal

   secrecy_of scn1       %% ok
   secrecy_of scn2      %% ok

   authentication_on b2_b1_oi1   %% ok, b2 aut b1 pe OI1 
   authentication_on b4_b2_oi2   %% ok, B4 aut B2 pe OI2 

   authentication_on pg_b1_pi1   %% ok,PG aut B1 pe PI1, in s12 
   authentication_on pg_b2_pi2   %% ok,pg aut B2 pe PI2, in s24

   authentication_on b2_pg_pe24  %% ok, B2 aut PG pe pe24=y
   authentication_on b2_pg_ape24  %% ok, B2 aut PG pe ape24
   authentication_on b2_pg_pe24a  %% ok, B2 aut PG pe pe24=a
   authentication_on b2_pg_pe12   %% ok, B2 aut PG pe pe12=y
   authentication_on b2_pg_pe12a  %% ok, B2 aut PG pe pe12=a
   authentication_on b2_pg_pe12a_pid1_pid2  %% ok, B2 aut PG pe pe12=a ptr cumpararea pid1 or pid2
   authentication_on b2_pg_ape12  %% ok, B2 aut PG pe ape12
   authentication_on b1_pg_pe12   %% ok, B1 aut PG pe pe12=y
                                    %% authentication_on b1_pg_e24 Nu treb ca nu se poate aut de B1, 
                                    %% fiind gen ptr B2...e implicit 
   authentication_on b1_pg_pe12a   %% ok, B1 aut PG pe pe12=a
   authentication_on b1_pg_pe12a_pid1_pid2  %% ok, B1 aut PG pe pe12=a ptr cumpararea pid1 or pid2
   authentication_on b1_pg_ape12   %% ok, b1 aut pg pe ape12, in cazul Res1


end goal

% Call of the main role:
environment()
